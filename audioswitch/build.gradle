apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'kotlin-android'
apply plugin: "org.jetbrains.dokka"
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion 34
    namespace "com.twilio.audioswitch"
    testNamespace "com.twilio.audioswitch.test"

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        buildConfigField("String", "VERSION_NAME",
                "\"${audioSwitchVersion}\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lint {
        baseline = file("lint-baseline.xml")
    }

    dokkaHtml.configure {
        dokkaSourceSets {
            named("main") {
                noAndroidSdkLink.set(false)
                includeNonPublic = false
                reportUndocumented = true
                skipEmptyPackages = true
            }
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation "androidx.annotation:annotation:1.6.0"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.1.0'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    def androidXTest = '1.5.0'
    androidTestUtil "androidx.test:orchestrator:$androidXTest"
    androidTestImplementation "androidx.test:runner:$androidXTest"
    androidTestImplementation "androidx.test:rules:$androidXTest"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
}

/*
 * The publishing block enables publishing to MavenCentral
 */
publishing {
    publications {
        audioSwitchRelease(MavenPublication) {

            groupId = 'com.twilio'
            artifactId = 'audioswitch'
            version = audioSwitchVersion

            pom {
                name = 'Audioswitch'
                description = 'An Android audio management library for real-time communication apps.'
                url = 'https://github.com/twilio/audioswitch'
                licenses {
                    license {
                        name = 'Apache 2.0'
                        url = 'https://github.com/twilio/audioswitch/blob/master/LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Twilio'
                        name = 'Twilio'
                    }
                }
                scm {
                    connection = 'scm:git:github.com/twilio/audioswitch.git'
                    developerConnection = 'scm:git:ssh://github.com/twilio/audioswitch.git'
                    url = 'https://github.com/twilio/audioswitch/tree/main'
                }
            }

            afterEvaluate {
                from components.release
            }
        }
    }
}

signing {
    sign publishing.publications
}
