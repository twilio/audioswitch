apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.artifactory"
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.annotation:annotation:1.1.0"
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    def androidXTest = '1.2.0'
    androidTestImplementation "androidx.test:runner:$androidXTest"
    androidTestImplementation "androidx.test:rules:$androidXTest"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.source
}

artifacts {
    archives androidSourcesJar
}

def audioSwitchGroupId = 'com.twilio'
def audioSwitchArtifactId = 'audioswitch'

/*
 * The publishing and artifactory blocks enable publishing AudioSwitch as snapshot to
 * JFrog OSS.
 */
publishing {
    publications {
        audioSwitchRelease(MavenPublication) {
            artifact("$buildDir/outputs/aar/audioswitch-release.aar")

            artifact androidSourcesJar {
                classifier "sources"
            }

            groupId = audioSwitchGroupId
            artifactId = audioSwitchArtifactId
            version = getVersionName()

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Audioswitch')
                root.appendNode('url', 'https://github.com/twilio/audioswitch')
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = jfrogUsername
            password = jfrogPassword
        }
        defaults {
            publications('audioSwitchRelease')
            publishArtifacts = true
            publishPom = true
        }
    }
    clientConfig.info.setBuildNumber(getShortCommitSha())
}

/*
 * The uploadArchives block is used to publish to Bintray
 */
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "${mavenRepo}") {
                authentication(
                        userName: mavenUsername,
                        password: mavenPassword
                )
            }
            pom.version = getVersionName()
            pom.groupId = audioSwitchGroupId
            pom.artifactId = audioSwitchArtifactId
            pom.packaging = 'aar'
        }
    }
}
